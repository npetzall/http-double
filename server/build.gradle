plugins {
    id 'me.champeau.gradle.jmh' version '0.2.0'
    id "com.jfrog.bintray" version "1.6"
}

apply plugin: 'osgi'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'maven-publish'

version '0.0.8'

dependencies {
    compile project(':api')
    compile 'io.netty:netty-codec-http:4.1.0.Beta8'
    compile 'io.netty:netty-handler:4.1.0.Beta8'
    compile 'org.slf4j:slf4j-api:1.7.13'
    compile 'io.dropwizard.metrics:metrics-core:3.1.2'
    compile 'io.dropwizard.metrics:metrics-jvm:3.1.2'
    compile 'io.dropwizard.metrics:metrics-json:3.1.2'
    runtime 'org.apache.logging.log4j:log4j-core:2.5'
    runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.5'
    testCompile 'org.testng:testng:6.9.9'
    testCompile 'org.assertj:assertj-core:3.1.0'
    testRuntime 'org.codehaus.sonar-plugins.java:sonar-jacoco-listeners:3.2'
}

test {
    useTestNG() {
        listeners << 'org.sonar.java.jacoco.TestNGListener'
    }
    testLogging.showStandardStreams = true
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }
}

jacoco {
    toolVersion = "0.7.5.201505241946"
}

test.finalizedBy jacocoTestReport

jmh {
    jmhVersion = '1.11.3'
}

publishing {
    publications {
        httpdouble(MavenPublication) {
            from components.java
            artifactId 'http-double-server'

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

bintray {
    user = project.getProperties().get("bintrayUser")
    key = project.getProperties().get("bintrayKey")
    publications = ['httpdouble']
    pkg {
        repo = 'maven'
        name = 'http-double'
        desc = 'The base server'
        licenses = ['MIT']
        websiteUrl = 'https://github.com/npetzall/http-double'
        issueTrackerUrl = 'https://github.com/npetzall/http-double/issues'
        vcsUrl = 'https://github.com/npetzall/http-double.git'
        githubRepo = 'npetzall/http-double'
    }
}
